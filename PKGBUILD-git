# Maintainer: Chris Warrick <aur@chriswarrick.com>
pkgbase=python-kwpbar-git
pkgname=('python-kwpbar-git' 'python2-kwpbar-git')
_pyname=kwpbar
_gitname=kwpbar.py
pkgver=0.1.0
pkgrel=1
pkgdesc='A progress bar for Python.'
arch=('any')
url='https://github.com/Kwpolska/kwpbar.py'
license=('???')
makedepends=('git' 'python' 'python2' 'python-setuptools' 'python2-setuptools')
options=(!emptydirs)
source=("git+https://github.com/Kwpolska/${_gitname}")
md5sums=('SKIP')

prepare() {
  cd "${srcdir}/${_gitname}"
  cp -R "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-py2"
}

package_python-kwpbar-git() {
  depends=('python' 'python-setuptools')
  provides=('python-kwpbar')
  conflicts=('python-kwpbar')
  cd "${srcdir}/${_gitname}"
  python3 setup.py install --root="${pkgdir}/" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgbase}/LICENSE"
}

package_python2-kwpbar-git() {
  depends=('python2' 'python2-setuptools')
  provides=('python2-kwpbar')
  conflicts=('python2-kwpbar')
  cd "${srcdir}/${_gitname}-py2"
  python2 setup.py install --root="${pkgdir}/" --optimize=1
}

pkgver() {
  cd "${srcdir}/${_gitname}"
  git describe --long | sed -E 's/([^-]*-g)/r\1/;s/-/./g;s/^v//'
}

# vim:set ts=2 sw=2 et:
